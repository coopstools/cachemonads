/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'jcooper' at '5/7/17 12:46 PM' with Gradle 2.13
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.13/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
apply plugin: 'maven'

group = 'com.coopstools'
version = '0.1.0'
archivesBaseName = 'cachemonads'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task pom {
    doLast {
        pom {
            project {
                name "com.coopstools:cachemonads"
                description "An extension of the java utils Stream and Optional, adding inline value storage and retrieval"
                url "https://github.com/coopstools/cachemonads"
                developers {
                    developer {
                        name "James R Cooper"
                        email "James@coopstools.com"
                        organization "coopstools"
                        organizationUrl "https://github.com/coopstools/"
                    }
                }
                scm {
                    connection "scm:git:git://github.com/coopstools/cachemonads.git"
                    developerConnection "scm:git:ssh://github.com:coopstools/cachemonads.git"
                    url "https://github.com/coopstools/cachemonads/tree/0.1.0"
                }
                inceptionYear '2008'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("$buildDir/libs/${archivesBaseName}-${version}.pom")
    }
}